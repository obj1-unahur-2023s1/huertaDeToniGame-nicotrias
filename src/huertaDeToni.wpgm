import wollok.game.*
import toni.*
import plantas.*
import pachamama.*

program huertaDeToni {
	game.width(15)
	game.height(15)
	game.ground("tierra.png")
	// Agregamos al amigo Toni.
	game.addVisualCharacter(toni)
	
	/*
	 * Acá usamos addVisualIn en vez de addVisual, y por esto 
	 * no es necesario que la pachamama conozca su position.
	 */
	game.addVisual(pachamama)
	
	/* 
	 * Agregamos una fila de maices. Ojo al regar, que Toni no va a conocer
	 * estas plantas.
	 * 
	 * En este ejemplo también usamos addVisualIn, por lo tanto
	 * el Maiz solo define la imagen.
	 * 
	 * De yapa, mostramos cómo agregar varios objetos de una utilizando
	 * un rango (1..13), que equivale a la lista [1, 2, 3, ..., 13].
	 */
	(1..13).forEach { x => 
		const maiz = new Maiz()
		toni.plantar(maiz)	
		game.addVisualIn(maiz, game.at(x, 5))
	}
	(1..13).forEach { x =>
		const trigo = new Trigo()
		toni.plantar(trigo)
		game.addVisualIn(trigo, game.at(x, 4))
	}
		
	keyboard.a().onPressDo {
		toni.regarLasPlantas()
	}		
		
	keyboard.z().onPressDo{
		pachamama.tirarBasura()
	}
	
	keyboard.t().onPressDo{
		const trigo = new Trigo()
		toni.plantar(trigo)
		game.addVisualIn(trigo, toni.position())		
	}
		
	keyboard.r().onPressDo{
		game.colliders(toni).forEach({p => p.regate()})
	}
	
	keyboard.m().onPressDo{
		const maiz = new Maiz()
		toni.plantar(maiz)	
		game.addVisualIn(maiz, toni.position())		
	}
	
	keyboard.x().onPressDo{
		toni.plantasListas().forEach({p=> game.removeVisual(p)})
		toni.cosecharTodo()
	}
	
	
	game.start()
}